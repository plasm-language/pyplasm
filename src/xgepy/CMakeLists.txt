
# huge file is generated!
IF (PYPLASM_WINDOWS)
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /bigobj")
ENDIF()

FIND_PACKAGE(PythonInterp ${PYTHON_REQUIRED_VERSION} REQUIRED) 
FIND_PACKAGE(PythonLibs   ${PYTHON_REQUIRED_VERSION} REQUIRED)
INCLUDE_DIRECTORIES(${PYTHON_INCLUDE_PATH})
MESSAGE("-- PYTHON_EXECUTABLE: ${PYTHON_EXECUTABLE}")

# find site package dir
EXECUTE_PROCESS(COMMAND ${PYTHON_EXECUTABLE} -c 
  "from distutils.sysconfig import get_python_lib; print get_python_lib()" 
  OUTPUT_VARIABLE PYTHON_SUGGESTED_SITE_PACKAGES_DIR OUTPUT_STRIP_TRAILING_WHITESPACE)
SET(PYTHON_SITE_PACKAGES_DIR ${PYTHON_SUGGESTED_SITE_PACKAGES_DIR})


SET(CMAKE_INSTALL_PREFIX "${PYTHON_SITE_PACKAGES_DIR}/pyplasm" )
MESSAGE("-- CMAKE_INSTALL_PREFIX: ${CMAKE_INSTALL_PREFIX}")

# find numpy (anaconda has already it)
IF (NOT ${PYPLASM_APPLE})
  SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}")
  FIND_PACKAGE(NumPy REQUIRED)
  INCLUDE_DIRECTORIES(${NUMPY_INCLUDE_DIRS})
ENDIF()

# use SWIG to generate file or not
SET(PYPLASM_REGENERATE_SWIG_WRAPPERS OFF CACHE BOOL "Regenerate swig wrappers")
IF (PYPLASM_REGENERATE_SWIG_WRAPPERS)
  MESSAGE("-- Regenerating swig wrappers")
  FIND_PACKAGE(SWIG REQUIRED)
  INCLUDE(${SWIG_USE_FILE})
  SET(CMAKE_SWIG_FLAGS "")
  SET_SOURCE_FILES_PROPERTIES(xge.i PROPERTIES CPLUSPLUS ON)
  # important -extranative (which define SWIG_PYTHON_EXTRA_NATIVE_CONTAINERS see http://old.nabble.com/Re%3A-std_vector-and-typemaps-p32605911.html)  
  SET_SOURCE_FILES_PROPERTIES(xge.i PROPERTIES SWIG_FLAGS "-threads;-extranative")
  SWIG_ADD_MODULE(xgepy python xge.i)
  ADD_DEPENDENCIES(_xgepy xge)
  SWIG_LINK_LIBRARIES(xgepy ${PYTHON_LIBRARIES} xge)
  INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/xgepy.py DESTINATION "${CMAKE_INSTALL_PREFIX}")
ELSE()
  MESSAGE("-- Using existing swig wrappers")
  ADD_LIBRARY(_xgepy SHARED xge.i xgePYTHON_wrap.cxx xgePYTHON_wrap.h xgepy.py)
  TARGET_LINK_LIBRARIES(_xgepy ${PYTHON_LIBRARIES} xge)
  ADD_DEPENDENCIES(_xgepy xge)
  INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/xgepy.py DESTINATION "${CMAKE_INSTALL_PREFIX}")
  IF (PYPLASM_WINDOWS)
    SET_TARGET_PROPERTIES(_xgepy PROPERTIES SUFFIX ".pyd")
  ELSE()
    SET_TARGET_PROPERTIES(_xgepy PROPERTIES PREFIX "")
    SET_TARGET_PROPERTIES(_xgepy PROPERTIES SUFFIX ".so")
  ENDIF()
ENDIF()

# install the shared library
if (PYPLASM_WINDOWS)
  GET_FILENAME_COMPONENT(PYTHON_DIR ${PYTHON_EXECUTABLE} PATH)
  INSTALL(TARGETS _xgepy DESTINATION ${PYTHON_DIR}/DLLs)
ELSE()
  INSTALL(TARGETS _xgepy DESTINATION ${CMAKE_INSTALL_PREFIX})
ENDIF()

# install other *.py files
INSTALL(FILES 
  ${CMAKE_CURRENT_SOURCE_DIR}/fenvs.py 
  ${CMAKE_CURRENT_SOURCE_DIR}/__init__.py 
  ${CMAKE_CURRENT_SOURCE_DIR}/viewer.py 
  DESTINATION "${CMAKE_INSTALL_PREFIX}")


# install examples
FILE(GLOB XGEPY_EXAMPLES "${CMAKE_CURRENT_SOURCE_DIR}/../../resources/examples/*")
INSTALL(FILES ${XGEPY_EXAMPLES} DESTINATION "${CMAKE_INSTALL_PREFIX}/examples/")

 

